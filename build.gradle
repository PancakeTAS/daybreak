// Apply plugins required for project
plugins {
    // Add java library
    id 'java-library'
}

// Update plugin information
base {
    group = "de.pancake"
    archivesName = "daybreak"
}

// Compile with UTF-8
compileJava.options.encoding = 'UTF-8'

// Compile for java 21
java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_21
}

// Add repositories required for project
repositories {
    // Add maven central for lombok
    mavenCentral()
    // Add papermc repository for api
    maven { url "https://papermc.io/repo/repository/maven-public/" }
    // Add codemc repository for api
    maven { url "https://repo.codemc.io/repository/maven-public/" }
}

// Add paper api
dependencies {
    // Add lombok processor
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    // Add paper mc api for compile time
    compileOnly "io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT"

    // Add chunky api for compile time
    compileOnly "org.popcraft:chunky-common:1.3.92"
}

// Upload plugin to server [login via key]
tasks.register('deployProduction', Exec) {
    workingDir "${buildDir}/libs"
    commandLine "cmd.exe /k echo put daybreak.jar | sftp mgnetwork:/home/p2r3/daybreak/plugins/".split(" ")
}

tasks.register('deployAlpha', Exec) {
    workingDir "${buildDir}/libs"
    commandLine "cmd.exe /k echo put daybreak.jar | sftp mgnetwork:/home/p2r3/daybreak-alpha/plugins/".split(" ")
}

tasks.register('restartAlpha', Exec) {
    workingDir "${buildDir}/libs"
    commandLine "ssh mgnetwork daybreakalpha restart".split(" ")
}

deployProduction.dependsOn = ['build']
deployProduction.group = 'daybreak'

deployAlpha.dependsOn = ['build']
deployAlpha.finalizedBy = ['restartAlpha']
deployAlpha.group = 'daybreak'
